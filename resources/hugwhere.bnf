where-clause = WHERE conds
conds = ac (OR ac)*
ac = sc (AND sc)*
sc = cc | pc | LB conds RB | LP conds RP
KID = #"(:[-\w]+\*?)?:[-\w]+"
cc = le CO re
pc = NOT? EXISTS subquery
<subquery> = LP SELECT cols FROM tabs where-clause? RP
<tabs> = tab (COMMA tab)*
<tab> = (FIELD | subquery ) ID? | join-clause | LP join-clause RP
<join-clause> = tab ((INNER | CROSS | NATURAL INNER? | (FULL | LEFT | RIGHT) OUTER?)? JOIN tab ON conds)+
cols = col (<COMMA> col)*
col = (FIELD | func | subquery | ce | STR | KID) ID? | #"([-\w]+\.)?\*"
<ce> = LP ce RP | col (O col)+
func = ID LP cols RP
<le> = (FIELD | func | subquery | ce) FIELD?
<re> = FIELD | func | subquery | ce | STR | KID | value-list
<value-list> = LP KID RP
<FIELD> = #"([-\w]+\.)*[-\w]+"
<NOT> = 'not'
<EXISTS> = 'exists'
<LP> = '('
<RP> = ')'
<LB> = '['
<RB> = ']'
<SELECT> = 'select'
<FROM> = 'from'
<WHERE> = 'where'
<COMMA> = ','
<DOT> = '.'
<ID> = #"[-\w]+"
<AND> = 'and'
<OR> = 'or'
<O> = '*' | '/' | '+' | '-' | '||'
<CO> = '=' | '>=' | '<=' | '!=' | '<>' | 'like' | 'is' | 'is' NOT? | NOT? 'in' | '>' | '<'
<STR> = #"'.+?'"
<NOCYCLE> = 'nocycle'
<CONNECT> = 'connect'
<BY> = 'by'
<START> = 'start'
<WITH> = 'with'
<GROUP> = 'group'
<HAVING> = 'having'
<ORDER> = 'order'
<SIBLINGS> = 'siblings'
<INNER> = 'inner'
<CROSS> = 'cross'
<NATURAL> = 'natural'
<FULL> = 'full'
<LEFT> = 'left'
<RIGHT> = 'right'
<OUTER> = 'outer'
<JOIN> = 'join'
<ON> = 'on'
