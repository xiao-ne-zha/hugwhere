(* 该语言的一个语句为任意多个参数(parameter)、块(block)和其他元素(other)的组合 *)
statement = (other | parameter | block)+
(* block 是由 `{{}}`包围的 任意多个参数(parameter)、块(block)和其他元素(other)的组合 *)
block = <'{{'> (other | parameter | block)+ <'}}'>
(* 参数是指hugsql中所允许的参数形式，使用优先级规则避免解析冲突：先尝试匹配带类型参数，再尝试匹配普通参数 *)
parameter = (PARA_TYPE PARA_NAME) / PARA_NAME
(* 参数类型，以冒号开头，后跟字母、连字符和可选的星号 *)
<PARA_TYPE> = <':'> #"[A-Za-z-]+\*?"
(* 参数名称，以冒号开头，后跟字母、数字、连字符和点号 *)
<PARA_NAME> = <':'> #"[-\w]+(\.[-\w]+)*"
(* 其他元素是指字符串或其它非字符串、非参数、非block开始结束符的元素的组合 *)
other = (STR | ANY)
(* 字符串类型，包括单引号和双引号字符串 *)
<STR> = SQUOTA_STR | DQUOTA_STR
(* 双引号字符串，支持转义引号 *)
<DQUOTA_STR> = #'"([^"]|((?<=\\)")|(""))*"'
(* 单引号字符串，支持转义引号 *)
<SQUOTA_STR> = #"'([^']|((?<=\\)')|(''))*'"
(* 任意其他字符序列，不包括引号、冒号和双大括号，但包括单独的大括号和PostgreSQL的类型转换操作符:: *)
<ANY> = #'[^\'":{}]+' | #'::\w+' | #'\{(?!\{)' | #'\}(?!\})'
